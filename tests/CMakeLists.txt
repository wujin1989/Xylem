cmake_minimum_required(VERSION 3.16)

project(tests LANGUAGES C)

function(add_xylem_test test_name)
    add_executable(test-${test_name} "test-${test_name}.c")
    target_link_libraries(test-${test_name} PUBLIC xylem)
    add_test(NAME ${test_name} COMMAND test-${test_name})

    if(XYLEM_ENABLE_ASAN)
        if(WIN32 AND CMAKE_C_COMPILER_ID MATCHES "MSVC")
            target_compile_options(test-${test_name} PRIVATE /fsanitize=address)
            target_link_options(test-${test_name} PRIVATE /fsanitize=address)
        endif()
        
        if(UNIX AND CMAKE_C_COMPILER_ID MATCHES "GNU|Clang")
            target_compile_options(test-${test_name} PRIVATE -fsanitize=address -fno-omit-frame-pointer)
            target_link_options(test-${test_name} PRIVATE -fsanitize=address)
        endif()
    endif()

    if(XYLEM_ENABLE_TSAN)
        if(UNIX AND CMAKE_C_COMPILER_ID MATCHES "GNU|Clang")
            target_compile_options(test-${test_name} PRIVATE -fsanitize=thread -fno-omit-frame-pointer)
            target_link_options(test-${test_name} PRIVATE -fsanitize=thread)
        endif()
    endif()

    if(XYLEM_ENABLE_UBSAN)
        if(UNIX AND CMAKE_C_COMPILER_ID MATCHES "GNU|Clang")
            target_compile_options(test-${test_name} PRIVATE -fsanitize=undefined -fno-omit-frame-pointer)
            target_link_options(test-${test_name} PRIVATE -fsanitize=undefined)
        endif()
    endif()

    if(XYLEM_ENABLE_COVERAGE)
        if(UNIX AND CMAKE_C_COMPILER_ID MATCHES "GNU|Clang")
            target_link_options(test-${test_name} PRIVATE --coverage)
        endif()
    endif()
endfunction()

add_xylem_test(heap)
add_xylem_test(rbtree)
add_xylem_test(varint)
add_xylem_test(waitgroup)